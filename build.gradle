buildscript {
  repositories {
    maven {
      url 'http://pdx-artifacts.pdx.vm.datanerd.us/repo'
    }
  }

  dependencies {
    classpath 'com.newrelic.agent.java:gradle-verify-instrumentation-plugin:1.0-SNAPSHOT'
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'idea'
  apply plugin: 'gradle-verify-instrumentation-plugin'

  sourceCompatibility = JavaVersion.VERSION_1_6
  targetCompatibility = JavaVersion.VERSION_1_6

  repositories {
    maven {
      url 'http://pdx-artifacts.pdx.vm.datanerd.us/repo'
    }
    maven {
      url 'https://repo1.maven.org/maven2'
    }
  }

  dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'com.newrelic.agent.java:instrumentation-test:4.7.0'
    testCompile 'com.newrelic.agent.java:newrelic-agent:4.7.0'
  }

  task install(dependsOn: jar, type: Copy) {
    description = 'Copies compiled jar to the NEW_RELIC_EXTENSIONS_DIR.'
    group  = 'New Relic'

    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR") ?: " "

    from jar
    into extDir
  }

  install.doFirst  {
    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR")
     if (extDir == null) {
         throw new Exception("Must set NEW_RELIC_EXTENSIONS_DIR.")
     }

     if (extDir.startsWith("~" + File.separator)) {
         extDir = System.getProperty("user.home") + extDir.substring(1);
     }

     if (!file(extDir).directory) {
         throw new Exception(extDir + "NEW_RELIC_EXTENSIONS_DIR, set as '" + extDir + "'is not a valid directory.")
     }
  }
}

